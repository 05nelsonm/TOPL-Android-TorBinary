apply plugin: 'com.android.library'
apply from: rootProject.file('gradle/maven-publish.gradle')

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
        buildConfigField 'int', 'TOR_BINARY_VERSION_CODE', "$versionCode"
        buildConfigField 'String', 'TOR_BINARY_VERSION_NAME', "\"$versionName\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

task copyTorBinaries(type: Copy) {
    from("$rootDir/external/tor-android/tor-android-binary/src/main/libs") {
        exclude 'arm64-v8a'
        exclude 'armeabi-v7a'
    }
    into("src/main/jniLibs")
    rename { filename ->
        filename.replace "$filename", 'libTor.so'
    }
}

task verifyChecksums(type: Exec, dependsOn: copyTorBinaries) {
    workingDir "$rootDir"
    commandLine "scripts/verify_checksums.sh"
}

gradle.projectsEvaluated {
    preBuild.dependsOn(verifyChecksums)
}