apply plugin: 'com.android.library'
apply from: rootProject.file('gradle/maven-publish.gradle')

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
        buildConfigField 'int', 'TOR_BINARY_VERSION_CODE', "$versionCode"
        buildConfigField 'String', 'TOR_BINARY_VERSION_NAME', "$versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

task buildTorBinaries(type: Exec) {
    workingDir "$rootDir"
    commandLine 'scripts/update_tor_binaries.sh', 'build'
}

task forceBuildTorBinaries(type: Exec) {
    workingDir "$rootDir"
    commandLine 'scripts/update_tor_binaries.sh', 'build', '--force'
}

task copyTorBinaries(type: Copy, dependsOn: buildTorBinaries) {
    from("$rootDir/external/tor-android/tor-android-binary/src/main/libs")
    into("src/main/jniLibs")
    rename { filename ->
        filename.replace "$filename", 'libTor.so'
    }
}

task copyTorBinariesNoBuild(type: Copy) {
    from("$rootDir/external/tor-android/tor-android-binary/src/main/libs")
    into("src/main/jniLibs")
    rename { filename ->
        filename.replace "$filename", 'libTor.so'
    }
}

task updateTorBinaries(type: Exec, dependsOn: copyTorBinaries) {
    workingDir "$rootDir"
    commandLine 'scripts/update_tor_binaries.sh', 'update_versions_file'
}

task checkForTorBinaryUpdate(type: Exec) {
    workingDir "$rootDir"
    commandLine 'scripts/update_tor_binaries.sh', 'check_for_update'
}

gradle.projectsEvaluated {
    preBuild.dependsOn(checkForTorBinaryUpdate)
}